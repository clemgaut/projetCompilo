/*
 *
 *       Auteurs: Boyer Alexis, Francois Thomas, Gautrais Clement
 *
 * 	creation : 13 fevrier 2002
 *
 *     nature : projet etudiant
 *
 */


options{
	JDK_VERSION="1.5";	
}

PARSER_BEGIN(Yaka)



public class Yaka implements Constante{

	public static TabIdent tabIdent = new TabIdent(10);
	public static Declaration declaration = new Declaration();
	public static Expression expression = new Expression();
	public static YVMasm yvm = new YVMasm();
	public static Iteration iter = new Iteration();
	public static int cptLigne = 1;
	public static int cptCondMax = 1, cptCondCourant = 1;
	public static ControleTypeParam controleTypeParam = new ControleTypeParam();

  public static void main(String args[]) { 
    Yaka analyseur;
    java.io.InputStream input;

    if (args.length==1) {
      System.out.print(args[args.length-1] + ": ");
      try {
        input = new java.io.FileInputStream(args[args.length-1]+yvm.EXTENSION);
      } catch (java.io.FileNotFoundException e) {
        System.out.println("Fichier introuvable.");
        return;
      }
    } else if (args.length==0) {
      System.out.println("Lecture sur l'entree standard...");
      input = System.in;
    } else {
      System.out.println("Usage: java Gram [fichier]");
      return;
    }
    try {
      analyseur = new Yaka(input);
      analyseur.analyse();
      System.out.println("analyse syntaxique terminee!");
    } catch (Exception e) {//Parse exception avant
      String msg = e.getMessage();
      if(msg!=null){
    	msg= msg.substring(msg.indexOf(System.getProperty("line.separator")));
    	System.out.println("ligne[" + cptLigne+ "] : Erreur de syntaxe : "+msg);
      }
      else
    	  System.out.println("ligne[" + cptLigne+ "] : Erreur de syntaxe");
      
    }
  } 
}

PARSER_END(Yaka)
/***************************************/
/********** TOKEN DEFINITIONS **********/
/***************************************/

TOKEN_MGR_DECLS :
{public static String identLu,chaineLue;
 public static int entierLu;}

/*** Skip whitespace and comments ***/
SKIP :
{
  " "
| "\t"
| "\n" {Yaka.cptLigne++;}
| "\r"
| "(*"   : IN_COMMENT
}
<IN_COMMENT> MORE:
{
  < ~[] >
}
<IN_COMMENT> SKIP:
{
   < "*)" >  {SwitchTo(DEFAULT);} 
}


/* Mots reserves*/

TOKEN :
{
  < BOOLEEN: "BOOLEEN" >
| < VAR: "VAR" >
| < FAIRE: "FAIRE" >
| < SINON: "SINON" >
| < POUR: "POUR" >
| < SI: "SI" >
| < FSI: "FSI">
| < ENTIER: "ENTIER" >
| < RETOURNE: "RETOURNE" >
| < VRAI: "VRAI" >
| < TANTQUE: "TANTQUE" >
| <CONST : "CONST">
| <FAUX : "FAUX">
| <FAIT : "FAIT">
| <OU : "OU">
| <ET : "ET">
| <NON : "NON">
| <FONCTION : "FONCTION">
| <FFONCTION  : "FFONCTION">
| <PROGRAMME : "PROGRAMME" >
| <FPROGRAMME: "FPROGRAMME" >
| <PRINCIPAL : "PRINCIPAL">
| <FPRINCIPAL : "FPRINCIPAL">
| <ALORS : "ALORS">
| <ECRIRE : "ECRIRE">
| <LIRE : "LIRE">
| <ALALIGNE : "ALALIGNE">
| <PLUS : "+">
| <MOINS : "-">
| <MUL : "*">
| <DIV : "/">
| <INF : "<">
| <SUP : ">">
| <INFE : "<=">
| <SUPE : ">=">
| <DIFF : "<>">
| <EGAL : "=">
| <ERREUR: "ERREUR" >

}



/*** unites de base nombres, idents,  strings ***/
TOKEN  :
{
  < #chiffre : ["0"-"9"] >
| < entier : (<chiffre>)+ > 
	{ entierLu = Integer.parseInt(image.toString());
        }
| < #lettre: ["A"-"Z","a"-"z"] >
| < ident : <lettre> (<lettre> | <chiffre>)* >
	{identLu =image.toString();	
	}
| < chaine : "\"" (~["\""])* "\"" | "'" (~["'"])* "'" >
	{ chaineLue = image.toString();
	 }
}


/**************************************/
/********debut de la grammaire ********/
/**************************************/
void analyse() : {}
{
   <PROGRAMME>  
      <ident>	{yvm.setFichier(YakaTokenManager.identLu + yvm.EXTENSION);
        yvm.entete();}
   (declFonction())*
   <PRINCIPAL> {yvm.debut();}
   bloc()
   <FPRINCIPAL>
   <FPROGRAMME> {yvm.queue();}
}

void declFonction() : {}
{
	typeFonction() <FONCTION> <ident> {if(Yaka.tabIdent.existeFonct(YakaTokenManager.identLu)){
										System.out.println("Erreur ligne["+cptLigne+"] : identifiant "+ YakaTokenManager.identLu +" deja present" );
										}else{
										DeclarationFonct.creerFonct(YakaTokenManager.identLu);
										yvm.etiquette(YakaTokenManager.identLu);
										}}
	paramForms() {DeclarationFonct.ajouterFonctCourante();}
	bloc() <FFONCTION> {yvm.fermeBloc(DeclarationFonct.getNbParam()*2);
						Yaka.tabIdent.effacer();
						DeclarationVar.initialisationCompteur();
						DeclarationParam.initialisationCompteur();}
}

void paramForms() : {}
{
	"(" (paramForm() ("," paramForm())* )? ")" {DeclarationParam.setNbParam(DeclarationFonct.getNbParam()*2);
												DeclarationParam.setOffset(Yaka.tabIdent.locaux);}
}

void paramForm() : {}
{
	typeParam() <ident> {if(!Yaka.tabIdent.existeIdent(YakaTokenManager.identLu)){
							DeclarationFonct.ajouterParam(DeclarationParam.getType());
							DeclarationParam.affecteNomIdent(YakaTokenManager.identLu);}
						else{
						System.out.println("Erreur ligne["+cptLigne+"] : parametre "+ YakaTokenManager.identLu +" deja present");
						}}
}

void bloc() : {}{
 (declConst())*
 (declVar())*
 {DeclarationVar.reserverMemoire();}
   suiteInstr() 
}

void declConst() : {}
{
  <CONST>  defConst() ( "," defConst())* ";"
}

void defConst() : {}
{
	
  <ident> { DeclarationConst.affecteNomIdent(YakaTokenManager.identLu);
  			if(DeclarationConst.existeIdentSommet()){
  				System.out.println("Erreur ligne["+cptLigne+"] : identifiant "+ YakaTokenManager.identLu +" deja present" );}}
   "=" valConst()
    
}

void valConst() : {}{
  <entier> 	{ DeclarationConst.ajouterIdentEntier(YakaTokenManager.entierLu);}	
 | <ident>	{ if(tabIdent.chercheIdent(YakaTokenManager.identLu)==null){
 					System.out.println("Erreur ligne["+cptLigne+"] : identifiant "+ YakaTokenManager.identLu +" non defini" );
 				}
 				else{
 					DeclarationConst.ajouterIdent(tabIdent.chercheIdent(YakaTokenManager.identLu));
 					}}
 | <VRAI> 	{ DeclarationConst.ajouterIdentBooleen(VRAI);}	
 | <FAUX> 	{ DeclarationConst.ajouterIdentBooleen(FAUX);}	
}


void declVar() : {}
{
  <VAR> type()
    	
  <ident> {if(Yaka.tabIdent.existeIdent(YakaTokenManager.identLu)){System.out.println("Erreur ligne["+cptLigne+"] : identifiant " + YakaTokenManager.identLu + " deja utilise");}
      		else{DeclarationVar.affecteNomIdent(YakaTokenManager.identLu);}}

    		
  ("," 
      <ident>  {if(Yaka.tabIdent.existeIdent(YakaTokenManager.identLu)){System.out.println("Erreur ligne["+cptLigne+"] : identifiant " + YakaTokenManager.identLu + " deja utilise");}
      		else{DeclarationVar.affecteNomIdent(YakaTokenManager.identLu);}}
  )* ";"{}
	
}

void type() :{}
{
   <ENTIER> {DeclarationVar.changerType(YakaConstants.ENTIER);}	
 | <BOOLEEN> {DeclarationVar.changerType(YakaConstants.BOOLEEN);}	
}

void typeParam() : {}
{
   <ENTIER> {DeclarationParam.changerType(YakaConstants.ENTIER);}	
 | <BOOLEEN> {DeclarationParam.changerType(YakaConstants.BOOLEEN);}	
}

void typeFonction() : {}
{
   <ENTIER> {DeclarationFonct.changerTypeResult(YakaConstants.ENTIER);}	
 | <BOOLEEN> {DeclarationFonct.changerTypeResult(YakaConstants.BOOLEEN);}	
}

/*
 * Syntaxe des instructions.
 */
 
 void suiteInstr() : {}
 {
 	instruction() (";" (instruction())?)*
 }
 
 void instruction() : {}
 {
 	  affectation()
 	| lecture()
 	| ecriture()
 	| iteration()
 	| conditionelle()
 	| retourne()
 }
 
void affectation() : {}
{
	<ident> {Ident temp = tabIdent.chercheIdent(YakaTokenManager.identLu);}
	<EGAL> expression() {if(temp == null || temp.getType() != expression.voirTypeSommet()){
							System.out.println("Erreur ligne["+cptLigne+"] : types incompatibles lors de l'affectation" );
							}else{yvm.istore((temp).getOffset());
								  //Permet de supprimer le type en sommet de pile
								  expression.depilerType();}}
} 

void lecture() : {}
{
	<LIRE> "(" <ident> ")" {Ident id = tabIdent.chercheIdent(YakaTokenManager.identLu);
				 			 if(id == null || !id.estVar()){System.out.println("Erreur ligne["+cptLigne+"] : "+id.getNom()+" n'est pas une variable");}
				 			 else{
				 			 	yvm.lireEnt(((IdVar)id).getOffset());
				 			 }
				 		   }
}

void ecriture() : {}
{
	( <ECRIRE> "(" (expression() {if(yvm.ecrire(expression.voirTypeSommet())==0){
									System.out.println("Erreur ligne["+cptLigne+"] : ecriture impossible");}	;}
	                | <chaine>   {yvm.ecrireChaine(YakaTokenManager.chaineLue);}
	               ) ")" )
	| <ALALIGNE> {yvm.aLaLigne();}
}

void iteration() : {}
{
	<TANTQUE> {iter.pileFait.push(iter.cptIteMax);
				yvm.faire(iter.cptIteMax);}
	expression() {if(expression.voirTypeSommet() != YakaConstants.BOOLEEN){
								System.out.println("Erreur ligne["+cptLigne+"] : expression non booleene");
							}}
	<FAIRE> {yvm.iffaux(iter.cptIteMax, "FAIT");
			iter.cptIteMax++;}
	suiteInstr()
	<FAIT> {
			yvm.gotoFaire(iter.pileFait.peek());
			yvm.fait(iter.pileFait.pop());}
}

void conditionelle() : {}
{
	<SI> {cptCondCourant = cptCondMax;
			cptCondMax++;}
	expression() {if(expression.voirTypeSommet() != YakaConstants.BOOLEEN){
								System.out.println("Erreur ligne["+cptLigne+"] : expression non booleene");
							}}
	<ALORS> {yvm.iffaux(cptCondCourant, "SINON");}
	suiteInstr() {yvm.gotoFSI(cptCondCourant);
				yvm.sinon(cptCondCourant);}
	(<SINON> suiteInstr())? 
	<FSI> {yvm.finSi(cptCondCourant);
			cptCondCourant--;}
}

void retourne() : {}
{
	<RETOURNE> expression() {if(expression.voirTypeSommet() != DeclarationFonct.getFonctCourante().getTypeResult()){	  							System.out.println("Erreur ligne["+cptLigne+"] : Type de retour incorrect");
	  					}else{  					  
	  							yvm.ireturn(DeclarationFonct.getNbParam()*2 + 4);}}	
}

/*
 * Expression .
 */
void expression() : {}
{
  simpleExpr() 
  (opRel() 
  simpleExpr() {expression.faireOperation();}
  )?
  {if(expression.voirTypeSommet() == Yaka.ERREUR)
  {
	System.out.println("Erreur ligne["+cptLigne+"] : evaluation de l'expression impossible");
  }
}
    
  
}

void simpleExpr() : {}
{
   terme() 
   (opAdd() 
   terme()	{expression.faireOperation();}
   )*
} 
void terme() : {}
{
  facteur() 
  (opMult() 
  facteur()	{expression.faireOperation();}
  )*
}
void facteur() : {}
{   primaire() 
  |  opNeg() primaire() {expression.faireOperation();}    
}

void primaire(): {}
{
   valeur()
 | "(" expression() ")" 
 
}

void valeur() : {}
{ <entier>  {expression.empilerType(ENTIER); 
			 yvm.iconst(YakaTokenManager.entierLu);}
			 
 | <ident> 	{	String fct = YakaTokenManager.identLu;
 					if(!tabIdent.existeFonct(YakaTokenManager.identLu)){
 					Ident id = tabIdent.chercheIdent(YakaTokenManager.identLu);
 			 		if(id == null){System.out.println("Erreur ligne["+cptLigne+"] : "+id.getNom()+" non existant");}
 			 		else{
 					expression.empilerType(id.getType());
 			 		if(id.estVar()){
 			 			yvm.iload(((IdVar)id).getOffset());
 			 		}else if(id.estConst()){
 			 			yvm.iconst(((IdConst)id).getValeur());
 			 		}else if(id.estParam()){
 			 			yvm.iload(((IdParam)id).getOffset());
 			 		}else{System.out.println("Erreur ligne["+cptLigne+"] : type non reconnu");}
 			 		}
 			 	}else{yvm.reserveRetour();}}
 	(argumentsFonction(){if(controleTypeParam.depilerParamFonction() != FIN_PARAM){
 							System.out.println("Erreur ligne["+cptLigne+"] : Nombre d'argument incompatible");
 						}})? {if(tabIdent.existeFonct(fct)) 						{yvm.call(fct);} 				}
 | <VRAI>   {expression.empilerType(BOOLEEN); 
			 yvm.iconst(VAL_VRAI);}
 | <FAUX>   {expression.empilerType(BOOLEEN); 
			 yvm.iconst(VAL_FAUX);}	
 
}

void argumentsFonction() : {}
{
	"(" {expression.empilerType(tabIdent.getFonct(YakaTokenManager.identLu).getTypeResult());
		if(!tabIdent.existeFonct(YakaTokenManager.identLu)){
			System.out.println("Erreur ligne["+cptLigne+"] : La fonction "+YakaTokenManager.identLu+" n'existe pas");
		}
		else{
			IdFonct fonct = tabIdent.getFonct(YakaTokenManager.identLu);
			controleTypeParam.empilerParamsFonction(fonct.getParametres());			
		}
		}
	
	(expression(){if(controleTypeParam.voirTypeSommet() == FIN_PARAM){
						System.out.println("Erreur ligne["+cptLigne+"] : Nombre d'arguments incompatibles");
					}
					else if(expression.depilerType()!=controleTypeParam.depilerParamFonction()){
					System.out.println("Erreur ligne["+cptLigne+"] : Types incompatibles");}} 
					
	("," expression(){if(controleTypeParam.voirTypeSommet() == FIN_PARAM){
						System.out.println("Erreur ligne["+cptLigne+"] : Nombre d'arguments incompatible");
					}
					else if(expression.depilerType()!=controleTypeParam.depilerParamFonction()){
					System.out.println("Erreur ligne["+cptLigne+"] : Types incompatibles");}}  )* )? ")"
}

void opRel() : {}
{
   <EGAL> 	{expression.empilerOperateur(EGAL);}
 | <DIFF> 	{expression.empilerOperateur(DIFF);}
 | <INF>	{expression.empilerOperateur(INF);}
 | <INFE>	{expression.empilerOperateur(INFE);}
 | <SUP>	{expression.empilerOperateur(SUP);}
 | <SUPE>	{expression.empilerOperateur(SUPE);}
} 

void opAdd() : {}
{
    <PLUS>   {expression.empilerOperateur(PLUS);}
  | <MOINS>  {expression.empilerOperateur(MOINS);}
  | <OU> 	 {expression.empilerOperateur(OU);}
}

void opMult() : {}
{
    <MUL>	{expression.empilerOperateur(MUL);}
  | <DIV>	{expression.empilerOperateur(DIV);}
  | <ET>	{expression.empilerOperateur(ET);}
} 

void opNeg() : {}
{  <MOINS> 	{expression.empilerOperateur(NEG);}
 | <NON>	{expression.empilerOperateur(NON);}
}


